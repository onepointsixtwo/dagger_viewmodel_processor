buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1"
    }
}

if (project == rootProject) {
    apply plugin: 'io.codearte.nexus-staging'
}
apply plugin: 'java-library'

allprojects {
    repositories {
        mavenCentral()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.squareup:javapoet:1.9.0'
    if (project == rootProject) {
        implementation 'com.onepointsixtwo:dagger_viewmodel:1.0.2'
    } else {
        implementation project(':dagger_viewmodel')
    }
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project == rootProject) {
    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'git@github.com:onepointsixtwo/dagger_viewmodel_processor'
                        groupId 'com.onepointsixtwo'
                        version '1.0.2'

                        scm {
                            connection 'scm:git:git@github.com:onepointsixtwo/dagger_viewmodel_processor.git'
                            developerConnection 'scm:git:git@github.com:onepointsixtwo/dagger_viewmodel_processor.git'
                            url 'git@github.com:onepointsixtwo/dagger_viewmodel_processor.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'onepointsixtwo'
                                name = 'John Kartupelis'
                                email = 'john.kartupelis@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }

    nexusStaging {
        username = ossrhUser
        password = ossrhPassword
        packageGroup = 'com.onepointsixtwo'
    }
}